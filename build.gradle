plugins {
    id 'fabric-loom' version '1.5-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

apply plugin: 'com.github.johnrengelman.shadow'
version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    maven { url = "https://jitpack.io/" }
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://maven.topi.wtf/releases" }
    maven { url = "https://maven.meteordev.org/releases" }
    maven { url = "https://maven.lavalink.dev/releases" }
    maven { url = "https://maven.lavalink.dev/snapshots" }
    maven { url = "https://repo.u-team.info" }
    ////
    maven { url = "https://repo.kelcuprum.ru/releases" }
    maven { url = "https://repo.kelcuprum.ru/snapshots" }
    maven { url = "https://repo.kelcuprum.ru/forks" }
    maven { url = "https://repo.kelcuprum.ru/test" }
}

configurations { shade }
dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API, ModMenu, Cloth Config
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "maven.modrinth:modmenu:${project.modmenu_version}"
    //
    implementation shade("dev.arbjerg:lavaplayer:${project.lavaplayer}")
    implementation shade("dev.lavalink.youtube:v2:${project.youtube_source}")
//    implementation shade("net.hycrafthd.lavaplayer:lavaplayer:${project.lavaplayer_u}")
    implementation shade("dev.lavalink.youtube:youtube-source:1.0.5")
    implementation shade("com.github.topi314.lavasrc:lavasrc:${project.lavasrc}")
    implementation shade("com.github.topi314.lavasrc:lavasrc-protocol:${project.lavasrc_protocol}")
    modImplementation("ru.kelcuprum.alinlib:AlinLib-fabric:${project.alinlib}")
    include(implementation("meteordevelopment:starscript:0.2.2"))

    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

    subprojects.each {
        implementation project(path: ":${it.name}", configuration: 'namedElements')
        implementation project(path: ":${it.name}", configuration: 'shade')
        include project("${it.name}:")
    }

}
loom {
    accessWidenerPath = file("src/main/resources/waterplayer.accesswidener")
}


processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

test {
    useJUnitPlatform()
}

// Produce additional distributions
java {
    withSourcesJar()
}

// Produce a fat-jar of all shaded dependencies
shadowJar {
    configurations = [project.configurations.shade]
    destinationDirectory.set(layout.buildDirectory.dir('devlibs'))
}

remapJar {
    dependsOn tasks.shadowJar
    inputFile.set(tasks.shadowJar.archiveFile)
}

components.java.withVariantsFromConfiguration(configurations.shadowRuntimeElements) { skip() }

jar {
    from("LICENSE.md") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}